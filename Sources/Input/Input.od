package com.pigeon3d

class Recognizer<P>(val tp : RecognizerType<_, P>) {
    static def apply<P>(tp : RecognizerType<LongRecognizer, P>,
        began : Event<P> -> bool, changed : Event<P> -> void, ended : Event<P> -> void)
        : Recognizer<P>
        = LongRecognizer<P>(tp, began, changed, ended, {})
    static def apply<P>(tp : RecognizerType<LongRecognizer, P>,
        began : Event<P> -> bool, changed : Event<P> -> void, ended : Event<P> -> void, canceled : Event<P> -> void)
        : Recognizer<P>
        = LongRecognizer<P>(tp, began, changed, ended, canceled)

    static def apply<P>(tp : RecognizerType<ShortRecognizer, P>, on : Event<P> -> bool)
        = ShortRecognizer<P>(tp, on)


    def add(recognizer : Recognizer<_>) : Recognizers = Recognizers([self, recognizer])
}
class LongRecognizer<P>(tp : RecognizerType<LongRecognizer, P>, val began : Event<P> -> bool,
    val changed : Event<P> -> void, val ended : Event<P> -> void, val canceled : Event<P> -> void) extends Recognizer<P>(tp)
class ShortRecognizer<P>(tp : RecognizerType<ShortRecognizer, P>, val on : Event<P> -> bool) extends Recognizer<P>(tp)


trait InputProcessor {
    def isProcessorActive : bool = !Director.current.isPaused.value
    def recognizers : Recognizers
}


class Recognizers(val items : [Recognizer<_>]) {
    static def apply(recognizer : Recognizer<_>) : Recognizers = Recognizers([recognizer])

    def on<P>(event : Event<P>) : ShortRecognizer<P>? = {
        items.find{item -> event.recognizerType == item.tp && item.cast<ShortRecognizer<P>>.on(event)}.cast<ShortRecognizer<P>?>
    }

    def began<P>(event : Event<P>) : LongRecognizer? = {
        items.find{item -> event.recognizerType == item.tp && item.cast<LongRecognizer<P>>.began(event)}.cast<LongRecognizer?>
    }

    def add(recognizer : Recognizer<_>) = Recognizers(items + recognizer)
    def add(recognizers : Recognizers) = Recognizers(items + recognizers.items)
    def types : Set<RecognizerType<_, _>> = items.chain.map(_.tp).toSet
}

class RecognizersState(val recognizers : Recognizers) {
    def process<P>(event : Event<P>) : bool =
        if(event.phase == EventPhase.on) on(event)
        else if(event.phase == EventPhase.began) began(event)
        else if(event.phase == EventPhase.ended) ended(event)
        else if(event.phase == EventPhase.changed) changed(event)
        else canceled(event)

    def on<P>(event : Event<P>) : bool = recognizers.on(event) != nil

    private val longMap = MHashMap<RecognizerType<LongRecognizer>, LongRecognizer<_>>()

    def began<P>(event : Event<P>) : bool = {
        val tp = event.recognizerType
        longMap.modify(key = tp, recognizers.began(event)) != nil
    }

    def changed<P>(event : Event<P>) : bool = longMap(event.recognizerType).map{rec ->
        rec.changed(event)
        true
    }.getOr(false)


    def ended<P>(event : Event<P>) : bool = {
        val tp = event.recognizerType
        longMap(tp)?.ended(event)
        longMap.remove(tp) != nil
    }

    def canceled<P>(event : Event<P>) : bool = {
        val tp = event.recognizerType
        longMap(tp)?.canceled(event)
        longMap.remove(tp) != nil
    }
}


abstract class RecognizerType<T extends Recognizer, P>

// For a computer it's mouse events. fingers 1 - left button, 2 - right
// For a touchpad "fingers + 1" is count of fingers
case class Pan(val fingers : uint) extends RecognizerType<LongRecognizer, Void> {
    static def apply : Pan = leftMouse
    static val leftMouse = Pan(1)
    static val rightMouse = Pan(2)
}

// For a computer it's click event. fingers 1 - left button, 2 - right
case class Tap(val fingers : uint = 1, val taps : uint = 1)  extends RecognizerType<ShortRecognizer, Void>

case class Pinch extends RecognizerType<LongRecognizer, PinchParameter>
case class PinchParameter(val scale : float, val velocity : float)

enum EventPhase {
    began()
    changed()
    ended()
    canceled()
    on()
}

trait Event<P> {
    def recognizerType : RecognizerType<_, P>
    def phase : EventPhase
    def locationInView : vec2
    def viewSize : vec2
    def param : P
    def matrixModel : MatrixModel = MatrixModel.identity
    def viewport : Rect = Rect(0, 0, 1, 1)

    def locationInViewport : vec2 = locationInView
    def location : vec2 = locationInView
    def locationFor(depth : float) : vec2 = locationInView
    def segment : Line3 = Line3(vec3(locationInView, 0), vec3(0, 0, 1000))
    def checkViewport : bool = true
}

class ViewEvent<P>(override val recognizerType : RecognizerType<_, P>, override val phase : EventPhase,
    override val locationInView : vec2, override val viewSize : vec2, override val param : P) extends Event<P>

class CameraEvent<P>(val event : Event<P>, override val matrixModel : MatrixModel, override val viewport : Rect) extends Event<P> {
    override val recognizerType : RecognizerType<_, P> = event.recognizerType
    override def phase : EventPhase = event.phase
    override val locationInView = event.locationInView
    override def viewSize : vec2 = event.viewSize
    override def param : P = event.param

    override def location : vec2 = locationFor(0)
    override def locationInViewport : vec2 = {
        (locationInView - viewport.p)/viewport.size*2 - vec2(1, 1)
    }
    override def locationFor(depth : float) : vec2 = segment.r(plane = Plane(vec3(0, 0, depth), vec3(0, 0, 1))).xy

    lazy val segment : Line3 = {
        val loc = locationInViewport
        val mat4 = matrixModel.wcp.inverse
        val p0 = mat4.mul(vec4 = vec4(loc.x, loc.y, -1, 1))
        val p1 = mat4.mul(vec4 = vec4(loc.x, loc.y, 1, 1))
        Line3(p0.xyz, p1.xyz - p0.xyz)
    }
    override def checkViewport : bool = viewport.contains(locationInView)
}


